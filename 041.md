neila

medium

# Reentrancy attack by `settlePoolUserMints()`

## Summary
Reentrancy attack by `settlePoolUserMints()`

## Vulnerability Detail
Can use reentrancy attack to withdraw all tokens in the contract by `settlePoolUserMint()`

The `settlePoolUserMints` does not follow the “Check-Effect-Interaction” pattern.

Ref: https://fravoll.github.io/solidity-patterns/checks_effects_interactions.html

This pattern is used to prevent from reentrancy attack

```solidity
function settlePoolUserMints(
    address user,
    PoolType poolType,
    uint256 poolTier
  ) public {
    /* ~~~ */

    /* ~~~ INTERACTION ~~~ */
    IPoolToken(pools[poolType][poolTier].fixedConfig.token).transfer(user, amountPoolTokenToMint);
    /* ~~~ EFFECT ~~~ */
    userAction_depositPaymentToken[user][poolType][poolTier] = userAction;
    /* ~~~ */
  }
}
```
By the way, the following code is not `EFFECT` section because `userAction` is declared by memory data location.

If the `pools[poolType][poolTier].fixedConfig.token` is ERC777 token, an attacker can execute reentrancy-attack and all funds can be stolen

```solidity
function settlePoolUserMints(
    address user,
    PoolType poolType,
    uint256 poolTier
  ) public {
    /* ~~~ */
    UserAction memory userAction = userAction_depositPaymentToken[user][poolType][poolTier];

    if (userAction.correspondingEpoch != 0 && userAction.correspondingEpoch <= epochInfo.latestExecutedEpochIndex) {
    uint256 poolToken_price = poolToken_priceSnapshot[userAction.correspondingEpoch][poolType][poolTier];
    uint256 amountPoolTokenToMint = uint256(userAction.amount).div(poolToken_price);

    // If secondary order exists
    if (userAction.nextEpochAmount > 0) {
      uint32 secondaryOrderEpoch = userAction.correspondingEpoch + 1;

      // If its possible to also execute the secondary order slot
      if (secondaryOrderEpoch <= epochInfo.latestExecutedEpochIndex) {
        // then also execute
        poolToken_price = poolToken_priceSnapshot[secondaryOrderEpoch][poolType][poolTier];
        amountPoolTokenToMint += uint256(userAction.nextEpochAmount).div(poolToken_price);

        userAction.amount = 0;
        userAction.correspondingEpoch = 0;
      } else {
        // If secondary order cannot be executed, bump it to the primary slot.
        userAction.amount = userAction.nextEpochAmount;
        userAction.correspondingEpoch = secondaryOrderEpoch;
      }
      // has to zero as either executed or bumped to primary slot
      userAction.nextEpochAmount = 0;
    } else {
      // If user has no pending mints then simply wipe
      userAction.amount = 0;
      userAction.correspondingEpoch = 0;
    }
		/* ~~~ */
}
```

## Impact
Setting the severity to medium is conditional to the token need to be ERC777 assumption, but the impact is the full loss of contract reward token holdings.

## Code Snippet
https://github.com/unchain-dev/2022-11-float-capital-UNCHAIN/blob/main/contracts/market/template/MarketCore.sol#L429-L489

```solidity
function settlePoolUserMints(
    address user,
    PoolType poolType,
    uint256 poolTier
  ) public {
  /*
    NOTE: please reflect any changes made to this function to the `getUsersConfirmedButNotSettledPoolTokenBalance` function too.
   Users can have mints in two consecutive epochs (with both not yet being executed). In this case we say both there primary,
   and seconday order slot are full. Once upkeep has been performed on those epochs, this function can be called at a later stage,
   asynchronusly (generally when a user mints again or uses their tokens), in order to calculate and send the tokens owed to the user
   based on their mints in the primiary and secondary slot (the secondary slot may not always exist).
   Corresponding epoch refers to the epoch associated with the primary slot action.
  */

  UserAction memory userAction = userAction_depositPaymentToken[user][poolType][poolTier];

  // Case if the primary order can be executed.
  if (userAction.correspondingEpoch != 0 && userAction.correspondingEpoch <= epochInfo.latestExecutedEpochIndex) {
    uint256 poolToken_price = poolToken_priceSnapshot[userAction.correspondingEpoch][poolType][poolTier];
    uint256 amountPoolTokenToMint = uint256(userAction.amount).div(poolToken_price);

    // If secondary order exists
    if (userAction.nextEpochAmount > 0) {
      uint32 secondaryOrderEpoch = userAction.correspondingEpoch + 1;

      // If its possible to also execute the secondary order slot
      if (secondaryOrderEpoch <= epochInfo.latestExecutedEpochIndex) {
        // then also execute
        poolToken_price = poolToken_priceSnapshot[secondaryOrderEpoch][poolType][poolTier];
        amountPoolTokenToMint += uint256(userAction.nextEpochAmount).div(poolToken_price);

        userAction.amount = 0;
        userAction.correspondingEpoch = 0;
      } else {
        // If secondary order cannot be executed, bump it to the primary slot.
        userAction.amount = userAction.nextEpochAmount;
        userAction.correspondingEpoch = secondaryOrderEpoch;
      }
      // has to zero as either executed or bumped to primary slot
      userAction.nextEpochAmount = 0;
    } else {
      // If user has no pending mints then simply wipe
      userAction.amount = 0;
      userAction.correspondingEpoch = 0;
    }

    //slither-disable-next-line unchecked-transfer
    IPoolToken(pools[poolType][poolTier].fixedConfig.token).transfer(user, amountPoolTokenToMint);

    userAction_depositPaymentToken[user][poolType][poolTier] = userAction;

    emit ExecuteEpochSettlementMintUser(
      MarketHelpers.packPoolId(poolType, uint8(poolTier)),
      user,
      epochInfo.latestExecutedEpochIndex,
      amountPoolTokenToMint
    );
  }
}
```

## Tool used

Manual Review

## Recommendation
Consider adding the `nonReentrant` modifier by OpenZeppelin

https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/security/ReentrancyGuard.sol#L50-L54

Consider following “Check-Effect-Interaction” patterns

The [settlePoolUserRedeems()](https://github.com/unchain-dev/2022-11-float-capital-UNCHAIN/blob/main/contracts/market/template/MarketCore.sol#L495-L541) implemented in this pattern already.