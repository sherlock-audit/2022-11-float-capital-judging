ctf_sec

medium

# _deprecateMarket can be execute multiple times

## Summary

_deprecateMarket can be execute multiple times 

## Vulnerability Detail

The _deprecateMarket should only be called once.

The code below does not check if the market is already deprecated, if the market deprecated is already deprecated, the function below should not be again, 

```solidity
  /// @notice This function will auto-deprecate the market if there are no updates for more than 10 days.
  /// @dev 10 days should be enough time for the team to make an informed decision on how to handle this error.
  function deprecateMarketNoOracleUpdates() external {
    require(((oracleManager.getCurrentEpochIndex() - epochInfo.latestExecutedEpochIndex) * oracleManager.EPOCH_LENGTH()) > 10 days);

    _deprecateMarket();
  }

  /// @notice Place the market in a state where no more price updates or mints are allowed
  function deprecateMarket() external adminOnly {
    _deprecateMarket();
  }
```

## Impact

the function _deprecateMarket make crucial state update to make sure the user can exit the market and redeem the fund they put as collateral.

```solidity
  /// @notice Place the market in a state where no more price updates or mints are allowed
  function _deprecateMarket() internal {
    ValueChangeAndFunding memory emptyValueChangeAndFunding;

    uint128[2] memory newEffectiveLiquidity = effectiveLiquidityForPoolType;

    // Here we rebalance the market twice with zero price change (so the pool tokens don't change price) but all outstanding
    for (uint32 i = 1; i <= 2; i++)
      (newEffectiveLiquidity, ) = _rebalancePoolsAndExecuteBatchedActions(
        epochInfo.latestExecutedEpochIndex + i,
        newEffectiveLiquidity,
        0,
        emptyValueChangeAndFunding
      );

    effectiveLiquidityForPoolType = newEffectiveLiquidity;
    epochInfo.latestExecutedEpochIndex += 2;
    marketDeprecated = true;
    mintingPaused = true;
    emit MarketDeprecation();
  }
```

If we advance the epochInfo.latestExecutedEpochIndex and effectiveLiquidityForPoolType, user cannot exit the market properly.

because we need the correct epochInfo.latestExecutedEpochIndex to calculate how much fund we refund user in _exitDeprecatedMarket.

```solidity
      // redeem all user's pool tokens
      IPoolToken poolToken = IPoolToken(pools[poolType][poolIndex].fixedConfig.token);
      uint256 balance = poolToken.balanceOf(user);
      if (balance > 0) {
        //slither-disable-next-line unchecked-transfer
        poolToken.transferFrom(user, address(this), balance);
        poolToken.burn(balance);

        uint256 amount = balance.mul(poolToken_priceSnapshot[epochInfo.latestExecutedEpochIndex][poolType][poolIndex]);
        ILiquidityManager(liquidityManager).transferPaymentTokensToUser(user, amount);
      }
```

## Code Snippet

https://github.com/sherlock-audit/2022-11-float-capital/blob/main/contracts/market/template/MarketCore.sol#L623-L635

https://github.com/sherlock-audit/2022-11-float-capital/blob/main/contracts/market/template/MarketCore.sol#L639-L662

## Tool used

Manual Review

## Recommendation

We recommend use modifier checkMarketNotDeprecated to make sure the _deprecateMarket can only be called once.

```solidity
  function _deprecateMarket() checkMarketNotDeprecated internal {
```
